plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

allprojects {
    group = 'com.github.lhotari'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '8'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }

        maven {
            url 'https://jitpack.io'
            content {
                // limits using the jitpack repository for specific artifacts
                includeGroup 'com.github.lhotari.dbcontainers'
            }
        }
    }
}

dependencies {
    implementation 'org.springframework.boot.experimental:spring-boot-starter-data-r2dbc'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    testImplementation 'io.r2dbc:r2dbc-postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.boot.experimental:spring-boot-test-autoconfigure-r2dbc'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc'
    testImplementation 'com.github.lhotari.dbcontainers:dbcontainer-spring:0.1.4'
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot.experimental:spring-boot-bom-r2dbc:0.1.0.M3'
        mavenBom 'org.testcontainers:testcontainers-bom:1.13.0'
        mavenBom 'io.r2dbc:r2dbc-bom:Arabba-SR2'
    }
}

task truncateTableCrash(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.truncatetable.EmbeddedYBTruncateTableCrashBugTest"
    }
    systemProperty 'yugabyteVersion', project.findProperty('yugabyteVersion') ?: '2.0.10.0-b4'
}

task truncateTableCrashLocalhost(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.truncatetable.ProvidedYBTruncateTableCrashBugTest"
    }
}

task truncateTableCrashPostgres(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.truncatetable.EmbeddedPostgresTruncateTableCrashBugTest"
    }
}

task jsonIndexBug(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.jsonindex.EmbeddedYBJsonIndexBugTest"
    }
    systemProperty 'yugabyteVersion', project.findProperty('yugabyteVersion') ?: '2.1.1.0-b2'
}

task readRestartBug(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.readrestart.EmbeddedYBReadRestartBugTest"
    }
    systemProperty 'yugabyteVersion', project.findProperty('yugabyteVersion') ?: '2.1.2.0-b10'
}

task readRestartBugSimpleQueryMode(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.readrestart.EmbeddedYBReadRestartBugTest"
    }
    systemProperty 'yugabyteVersion', project.findProperty('yugabyteVersion') ?: '2.1.2.0-b10'
    // see preferQueryMode in https://jdbc.postgresql.org/documentation/head/connect.html
    systemProperty 'readrestartbug.preferQueryMode', 'simple'
}

task readRestartBugStaticSql(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.readrestart.EmbeddedYBReadRestartBugTest"
    }
    systemProperty 'yugabyteVersion', project.findProperty('yugabyteVersion') ?: '2.1.2.0-b10'
    systemProperty 'readrestartbug.staticsql', 'true'
}

task readRestartBugPostgres(type: Test) {
    classpath = test.classpath
    filter {
        includeTestsMatching "com.github.lhotari.dbcontainer.yugabyte.readrestart.EmbeddedPostgresReadRestartBugTest"
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        // enable verbose test logging by default
        events = org.gradle.api.tasks.testing.logging.TestLogEvent.values().toList().toSet()
    }
    outputs.upToDateWhen { false }
}
